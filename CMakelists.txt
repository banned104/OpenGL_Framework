cmake_minimum_required(VERSION 3.15)
project(main_opengl)

set(TARGET_NAME "${PROJECT_NAME}")
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# 编译选项: 是否编译为共享库(.so)
option(BUILD_AS_SHARED "Build as shared library for Android" OFF)

# -------------------------------------------------------
# Component源文件
set(COMPONENT_SOURCES
    Component/renderers/triangle_render.cpp
    Component/shader.cpp
    Component/camera/Camera.cpp
)


# -------------------------------------------------------
# Android编译配置 (编译为.so库) 
# -------------------------------------------------------
if(ANDROID OR BUILD_AS_SHARED)
    message(STATUS "Building for Android as shared library")
    
    # 添加JNI入口文件
    set(ANDROID_SOURCES
        native_renderer.cpp
    )
    
    # 创建共享库
    add_library(${TARGET_NAME} SHARED
        ${COMPONENT_SOURCES}
        ${ANDROID_SOURCES}
    )
    
    # Android平台链接库
    target_link_libraries(${TARGET_NAME}
        PRIVATE
        GLESv3
        EGL
        android
        log
    )
    
    # 包含目录
    target_include_directories(${TARGET_NAME}
        PRIVATE
        ${CMAKE_SOURCE_DIR}/3rdparty
        ${CMAKE_SOURCE_DIR}/Component
        ${CMAKE_SOURCE_DIR}/Component/renderers
        ${CMAKE_SOURCE_DIR}/Component/camera
        ${CMAKE_SOURCE_DIR}/shaders
    )

# -------------------------------------------------------
# PC编译配置 (编译为可执行文件)
# -------------------------------------------------------
else()
    message(STATUS "Building for PC as executable")
    
    # Find Python for GLSL conversion
    find_package(Python3 COMPONENTS Interpreter REQUIRED)

    # Define GLSL shader files and their outputs
    set(SHADER_DIR "${CMAKE_SOURCE_DIR}/shaders")
    set(GLSL_CONVERTER "${SHADER_DIR}/Convert_GLSL_to_h.py")

    # Define shader files to convert
    set(SHADER_FILES
        "${SHADER_DIR}/triangle/triangle.vert.glsl"
        "${SHADER_DIR}/triangle/triangle.frag.glsl"
        "${SHADER_DIR}/cube/cube.vert.glsl"
        "${SHADER_DIR}/cube/cube.frag.glsl"
    )
    set(PYTHON_ARGS "--pc")

    # Generate header files for each shader
    set(GENERATED_HEADERS)

    foreach(SHADER_FILE ${SHADER_FILES})
        get_filename_component(SHADER_BASENAME ${SHADER_FILE} NAME)
        # 获取Shader所在的文件目录
        get_filename_component(SHADER_DIR_PATH ${SHADER_FILE} DIRECTORY)
        string(REGEX REPLACE "\\.glsl$" "" SHADER_NAME "${SHADER_BASENAME}")
        # 将生成文件放在Shader所在的目录下
        set(HEADER_FILE "${SHADER_DIR_PATH}/${SHADER_NAME}.core.h")
        
        list(APPEND GENERATED_HEADERS ${HEADER_FILE})
        
        add_custom_command(
            OUTPUT ${HEADER_FILE}
            COMMAND ${Python3_EXECUTABLE} ${GLSL_CONVERTER} ${SHADER_FILE} ${HEADER_FILE} ${PYTHON_ARGS}
            DEPENDS ${SHADER_FILE} ${GLSL_CONVERTER}
            COMMENT "Converting ${SHADER_FILE} to C++ header"
            VERBATIM
        )
    endforeach()

    add_custom_target(generate_shaders
        DEPENDS ${GENERATED_HEADERS}
        COMMENT "Generating all shader header files"
    )

    # 添加第三方库子目录
    add_subdirectory(3rdparty/glad)
    add_subdirectory(3rdparty/glfw)

    # 创建可执行文件
    add_executable(${TARGET_NAME} 
        main.cpp
        ${COMPONENT_SOURCES}
    )

    # 查找OpenGL
    find_package(OpenGL REQUIRED)

    # 链接库
    target_link_libraries(${TARGET_NAME} 
        PRIVATE
        glfw
        glad
        OpenGL::GL
    )

    # 包含目录
    target_include_directories(${TARGET_NAME} 
        PRIVATE
        ${CMAKE_SOURCE_DIR}/3rdparty/glad/include
        ${CMAKE_SOURCE_DIR}/3rdparty/glfw/include
        ${CMAKE_SOURCE_DIR}/3rdparty
        ${CMAKE_SOURCE_DIR}/Component
        ${CMAKE_SOURCE_DIR}/Component/renderers
        ${CMAKE_SOURCE_DIR}/Component/camera
        ${CMAKE_SOURCE_DIR}/shaders
    )

    # 确保shader头文件在编译前生成
    add_dependencies(${TARGET_NAME} generate_shaders)
endif()